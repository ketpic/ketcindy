incenter(plist):=(
  regional(pA,pB,pC,a,b,c,pI,tmp);
  pA=plist_1; pB=plist_2; pC=plist_3;
  a=|pB-pC|;
  b=|pC-pA|;
  c=|pA-pB|;
  pI=(a*pA+b*pB+c*pC)/(a+b+c);
  pI;
);

inradius(plist):=(
  regional(r,pI,tmp);
  pI=incenter(plist);
  tmp=plist_3-plist_1;
  tmp=Crossprod(tmp,pI-plist_1)/|tmp|;
  r=|tmp|;
  r;
);

internal(pP):=(
  regional(out,nn);
  //global tL,cL,rL;
  out=0;
  nn=1;
  while(nn<=length(tL),
    if(|pP-cL_nn|<rL_nn,
	  out=nn;
	  nn=length(tL);
	);
	nn=nn+1;
  );
  out
);


Mkcrfig():=(
  regional(pI,r,nk,nl,tmp1,tmp2);
//  global all,tL,sL,sLg,rL,cL,dg1,dg2,flipok;
  GCLIST=[];
  Circledata("0",[A,B],[]);
  Listplot("0",append(all,all_1));
  forall(1..(length(all)),
    tmp1=0; tmp2=0;
    if((all_#).x<0,tmp1=-0.3);
    if((all_#).y<0,tmp2=-0.4);
    Drwletter((all_#).xy+[tmp1,tmp2],#,15);
  );
  rL=[];
  cL=[];
  forall(1..(length(tLg)),
    pI=incenter(tLg_#);
    r=inradius(tLg_#);
    rL=append(rL,r);
    cL=append(cL,pI);
    Circledata(text(#),[pI,r]);
  );
  forall(1..(length(sLg)),
    Listplot(text(#),sLg_#,["Color=royalblue"]);
  );
);

Polygondata():=(
  regional(tmp,tmp1,tmp2);
  //global nall,all,tL,tLg,sL,sLg
  tmp=Textedit(6,"6");
  subsedit(6,"N="+tmp);
  nall=parse(tmp);
  all=vL_(1..nall);
  tmp1=Textedit(7,"1-2-3,1-3-4,1-4-6,4-5-6");
  tmp=tokenize(tmp1,",");
  if(nallnow!=nall,
    if(length(tmp)!=nall-2,
      tmp1="";
      forall(2..(nall-1),
        tmp1=tmp1+"1-"+text(#)+"-"+text(#+1)+",";
      );
      tmp1=substring(tmp1,0,length(tmp1)-1);
      Subsedit(7,tmp1);
    );
    nallnow=nall;
  );
  tmp1=tokenize(tmp1,",");
  tL=apply(tmp1,tokenize(#,"-"));
  tLg=apply(tL,all_#);
  sL=[];
  forall(tL,tr,
    tmp=pairs(tr);
    tmp1=select(tmp,#_2!=#_1+1);
    sL=concat(sL,tmp1);
  );
  sL=set(sL);
  sLg=apply(sL,all_#);
);

isflippable():=(
  regional(nq,nr,flipok,tmp,tmp1,tmp2);
  //global tLg, Q, L
  flipok=0;
  nq=internal(Q);
  nr=internal(R);
  if((nq>0)&(nr>0),
    dg1=common(tL_nq,tL_nr);
    dg1=sort(dg1);
    if(length(dg1)==2,
      Shade(["cr"+nq],["Color=cyan"]);
      Shade(["cr"+nr],["Color=cyan"]);
      Pointdata("1",[Q,R],["Color=red","Size=3","notex"]);
      tmp1=remove(tL_nq,dg1);
      tmp2=remove(tL_nr,dg1);
      dg2=concat(tmp1,tmp2);
      dg2=sort(dg2);
      flipok=1;
    );
  );
  flipok;
);

Doflip():=(
  regional(tmp,tmp1,tmp2);
  //global sL,sLg,tL,tLg
  if(flipok==1,
    sL=remove(sL,[dg1]);
    sL=concat(sL,[dg2]);
    sL=apply(sL,sort(#));
    sLg=apply(sL,all_#);
    tmp1=select(tL,common(#,dg1)==dg1);
    tL=remove(tL,tmp1);
    tmp=remove(tmp1_1,dg2);
    tmp2=apply(tmp,append(dg2,#));
    tL=concat(tL,tmp2);
    tL=apply(tL,sort(#));
    tLg=apply(tL,all_#);
    tmp=apply(tL,text(#));
    tmp=apply(tmp,replace(#," ",""));
    tmp=apply(tmp,replace(#,"[",""));
    tmp=apply(tmp,replace(#,"]",""));
    tmp=apply(tmp,replace(#,",","-"));
    tmp1="";
    forall(tmp,tmp1=tmp1+#+",");
    tmp=substring(tmp1,0,length(tmp1)-1);
    Subsedit(7,tmp);
    Mkcrfig();
  );
);

